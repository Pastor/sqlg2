description = 'SQLG client/server example'

apply plugin: 'java'

apply from: '../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

dependencies {
    compile 'com.github.osobolev.sqlg2:sqlg2-full:4.0'
    compile 'org.eclipse.jetty.aggregate:jetty-all-server:8.1.17.v20150415'
    compile 'javax.servlet:javax.servlet-api:3.0.1'
    runtime driverClasspath
}

task preprocess << {
    ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.runtime.asPath)
    ant.sqlg(classpath: configurations.runtime.asPath,
             driverclass: jdbcDriver, dbclass: dbClass,
             url: jdbcUrl, user: username, password: password,
             implpack: 'wrapper', wrappack: 'wrapper', srcroot: 'src') {
        fileset(dir: 'src') {
            include(name: 'remote/dao/RemoteDAO.java')
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

task runServer(type: JavaExec, dependsOn: classes) {
    main = 'remote.Server'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, dbClass].collect {
        it == '' ? '""' : it
    })
}

task runClient(type: JavaExec, dependsOn: classes) {
    main = 'remote.Client'
    classpath = sourceSets.main.runtimeClasspath
    args([username, password].collect {
        it == '' ? '""' : it
    })
}

task run(type: JavaExec, dependsOn: classes) {
    main = 'remote.Main'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, username, password, dbClass].collect {
        it == '' ? '""' : it
    })
}

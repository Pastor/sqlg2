description = 'SQLG connection pool usage example'

apply plugin: 'java'

apply from: '../../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

dependencies {
    compile 'com.github.osobolev.sqlg2:sqlg2-full:4.0'
    compile 'com.jolbox:bonecp:0.8.0.RELEASE'
    runtime driverClasspath
    runtime 'org.slf4j:slf4j-nop:1.7.12'
}

task preprocess << {
    ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.runtime.asPath)
    ant.sqlg(classpath: configurations.runtime.asPath,
             driverclass: jdbcDriver, dbclass: dbClass,
             url: jdbcUrl, user: username, password: password,
             implpack: 'wrapper', wrappack: 'wrapper', srcroot: 'src') {
        fileset(dir: 'src') {
            include(name: 'pool/dao/PoolDAO.java')
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

task run(type: JavaExec, dependsOn: classes) {
    main = 'pool.PoolTest'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, username, password, dbClass].collect {
        it == '' ? '""' : it
    })
}

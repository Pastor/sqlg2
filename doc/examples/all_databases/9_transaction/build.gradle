description = 'SQLG transaction example'

apply plugin: 'java'

apply from: '../../db.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src']

dependencies {
    compile 'com.github.osobolev.sqlg2:sqlg2-full:4.0'
    runtime driverClasspath
}

task preprocess << {
    ant.taskdef(name: 'sqlg', classname: 'sqlg2.Preprocess', classpath: configurations.runtime.asPath)
    ant.sqlg(classpath: configurations.runtime.asPath,
             driverclass: jdbcDriver, dbclass: dbClass,
             url: jdbcUrl, user: username, password: password,
             implpack: 'wrapper', wrappack: 'wrapper', srcroot: 'src') {
        fileset(dir: 'src') {
            include(name: 'transaction/dao/CreateRow.java')
        }
    }
}
preprocess.onlyIf { doPreprocess }
compileJava.dependsOn(preprocess)

clean << {
    copy {
        from 'src/transaction/dao/CreateRow.orig' into 'src/transaction/dao' rename('CreateRow.orig', 'CreateRow.java')
    }
}

task run(type: JavaExec, dependsOn: classes) {
    main = 'transaction.TransactionTest'
    classpath = sourceSets.main.runtimeClasspath
    args([jdbcDriver, jdbcUrl, username, password, dbClass].collect {
        it == '' ? '""' : it
    })
}
